0  ST$ = "S6,D1,V254"
1  REM MANUAL SCAN A PORTION OF THE SCREEN AND SAVE AS A FAST SHAPE
2  REM THIS WORKS SUCH AS IT IS, BUT WAS NEVER REALLY FINISHED
3  REM FAST SHAPE WILL RE-USE LOCATION $4C00 AFTER SDP.INTRP IS DONE
10 HGR : HOME : VTAB 21 : INPUT "USE (P)ICTURE BUFFER OR (S)DP FILE? ";A$
20 IF A$="P" THEN GOSUB 300
30 IF A$="S" THEN GOSUB 400
35 REM FAST SHAPE BINARIES MUST BE LOADED AFTER SDP.INTRP FINISHES
36 PRINT "INSERT SDP IN ";ST$ : INPUT "PRESS ENTER WHEN READY";A$
37 PRINT  CHR$ (4);"BLOAD SI,A$4000,";ST$
38 PRINT  CHR$ (4);"BLOAD DR,A$300,";ST$
40  HOME
41 X = 0:Y = 0
42  POKE 8192,255 -  PEEK (8192)
43 I = 8192
45  VTAB 21
50  PRINT "MANEUVER SCAN BAR TO STARTING POSITION"
51  PRINT "PRESS S TO BEGIN ";
60  GET A$
70  IF A$ = "Y" THEN Y = Y - 1
72  IF A$ = "J" THEN X = X + 7
74  IF A$ = " " THEN Y = Y + 1
76  IF A$ = "G" THEN X = X - 7
78  IF A$ = "S" THEN 200
80  POKE 776,Y: POKE 778, INT (X / 256): POKE 777,X -  INT (X / 256) * 256: CALL 16384
90  POKE I,255 -  PEEK (I)
100 I =  PEEK (784) + 256 *  PEEK (785)
110  POKE I,255 -  PEEK (I)
120  GOTO 60

200 BY = 19456
201  POKE BY,3: POKE BY + 1,255 -  PEEK (I)
202 BY = BY + 2
205  HOME
206  VTAB 21: PRINT "BYTES USED: ";BY - 19456;""
208  GET A$
211  IF A$ = "Q" THEN 2000
212  IF A$ = "J" THEN X = X + 7
214  IF A$ = "G" THEN X = X - 7
216  IF A$ = " " THEN Y = Y + 1
218  POKE 776,Y: POKE 778, INT (X / 256): POKE 777,X -  INT (X / 256) * 256: CALL 16384
220 I =  PEEK (784) + 256 *  PEEK (785)
225  POKE I,255 -  PEEK (I)
231  IF A$ = "J" THEN  POKE BY,2
232  IF A$ = " " THEN  POKE BY,3
233  IF A$ = "G" THEN  POKE BY,4
235  POKE BY + 1,255 -  PEEK (I)
240 BY = BY + 2
250  GOTO 206

300  REM LOAD A PICTURE BUFFER
310  GOSUB 3000
320  PRINT : PRINT : PRINT CHR$(4);"BLOAD ";A$;",A$2000,S";S;",D";D;",V";V
330  RETURN

400  REM LOAD SDP GRAPHICS
410  POKE 232,0: POKE 233,64: REM SHAPE ADDR
420  SCALE=1: ROT=0
430  INPUT "IS DRAWING SQUEEZED (Y/N) ? ";SQ$
440  IF SQ$="Y" THEN I$ = "SDP.INTRP.E"
445  IF SQ$="N" THEN I$ = "SDP.INTRP"
450  PRINT : PRINT : PRINT CHR$(4);"BLOAD ";I$;",A$4B00,";ST$
460  PRINT  CHR$ (4);"BLOAD D0,A$4000,";ST$ : REM SHAPES
470  GOSUB 3000
480  PRINT : PRINT : PRINT CHR$(4);"BLOAD ";A$;",A$4C00,S";S;",D";D;",V";V
490  CLR = 1 : A1 = 0 : A2 = 76 : GOSUB 1000 : RETURN

1000  REM INVOKE SDP INTERPRETER
1010  POKE 768,189: POKE 769,A1: POKE 770,A2: POKE 771,96: REM LDA,X A1 A2 RTS
1020  IF CLR THEN CALL 19200
1030  IF NOT CLR THEN CALL 19203
1040  RETURN

2000  REM SAVE AND QUIT
2005  HOME: VTAB 21: INPUT "SAVE FAST SHAPE (Y/N) ? ";A$
2006  IF A$="Y" THEN 2010
2007  IF A$="N" THEN 2050
2008  GOTO 2005
2010  GOSUB 3000
2020  POKE BY,96
2030  BY = BY + 2
2040  PRINT : PRINT : PRINT  CHR$ (4);"BSAVE ";A$;",A19456,L";BY - 19456;",S";S;",D";D;",V";V
2050  TEXT : HOME
2060  PRINT "INSERT SDP IN ";ST$
2070  INPUT "PRESS ENTER WHEN READY";A$
2080  PRINT : PRINT : PRINT CHR$(4);"RUN HELLO,";ST$

3000  REM CHOOSE MASS STORAGE
3001  HOME : VTAB 21: INPUT "FILENAME: ";A$
3005  INPUT "SLOT (6): ";B$
3010  S =  VAL (B$)
3020  IF B$ = "" THEN S = 6
3030  INPUT "DRIVE (1): ";B$
3040  D =  VAL (B$)
3050  IF B$ = "" THEN D = 1
3060  INPUT "VOLUME (254): ";B$
3070  V =  VAL (B$)
3080  IF B$ = "" THEN V = 254
3085  INPUT "PRESS ENTER WHEN READY";B$
3090  RETURN
