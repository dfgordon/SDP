 1  POKE 232,0: POKE 233,64: REM SHAPE ADDR
 2  SCALE= 1: ROT= 0
 3  REM PICTURE WILL BE STORED AT 0X4C00
 4  REM WE ALSO USE A FEW BYTES AT 0X300
 5  PRINT  CHR$ (4);"BLOAD SDP.INTRP,A$4B00": REM INTERPRETER
 6  PRINT  CHR$ (4);"BLOAD D0,A$4000": REM SHAPES
 10  HGR
 30  NS = 1: MV = 18
 50  X = 139: Y = 79
 60  C = 1
 70  BY = 19456
 80  VTAB 21: INPUT "ENCODE SHAPE SELECTION (Y/N) ? ";A$
 81  IF A$ = "Y" THEN GOSUB 5100
 90  INPUT "LOAD A PICTURE BUFFER (Y/N) ? ";A$
 91  IF A$ = "Y" THEN GOSUB 1800

 100  REM POINT MODE LOOP
 101  HOME
 105  VTAB 21: XDRAW NS AT X,Y
 110  PRINT "X=";X;"     Y=";Y;"    "
 111  PRINT "BYTES USED: ";BY - 19456;"   "
 120  GET A$: XDRAW NS AT X,Y
 121  IF A$ = "]" THEN  POKE  - 16302,0: REM FULL SCREEN
 122  IF A$ = "[" THEN  POKE  - 16301,0: REM MIXED SCREEN
 130  IF A$ = "Y" THEN  GOSUB 200: REM UP
 131  IF A$ = "G" THEN  GOSUB 200: REM LEFT
 132  IF A$ = "J" THEN  GOSUB 200: REM RIGHT
 133  IF A$ = " " THEN  GOSUB 200: REM DOWN
 134  IF A$ = "1" THEN  GOSUB 200: REM LEFT 5
 135  IF A$ = "2" THEN  GOSUB 200: REM RIGHT 5
 136  IF A$ = "3" THEN  GOSUB 200: REM DOWN 5
 137  IF A$ = "4" THEN  GOSUB 200: REM UP 5
 138  IF A$ = "B" THEN GOSUB 300: REM BRUSH
 139  IF A$ = "P" THEN GOSUB 400: REM DRAW POINT
 140  IF A$ = "C" THEN GOSUB 500: REM COLOR
 141  IF A$ = "L" THEN GOSUB 600: REM LINE MODE
 142  IF A$ = "F" THEN GOSUB 900: REM FILL MODE
 150  IF A$ = "U" THEN GOSUB 1200: REM UNDO
 151  IF A$ = "M" THEN GOSUB 1300: REM MULTIPLE UNDO
 155  IF A$ = "<" THEN GOSUB 1400: REM REWIND AND PATCH
 156  IF A$ = ">" THEN GOSUB 1500: REM FAST FORWARD
 158  IF A$ = "S" THEN GOSUB 1600: REM DRAW S STEPS
 160  IF A$ = "Z" THEN GOSUB 1700: REM LINEAR REPEAT
 185  IF A$ = "I" THEN GOSUB 8000: REM INSERT EXISTING PICTURE
 190  IF A$ = "Q" THEN 30000: REM QUIT
 199  GOTO 105

 200  REM TRANSLATE POSITION
 204  DX = 0 : DY = 0
 210  IF A$ = "Y" THEN DY=-1
 211  IF A$ = "G" THEN DX=-1
 212  IF A$ = "J" THEN DX=1
 213  IF A$ = " " THEN DY=1
 220  IF A$ = "1" THEN DX=-5
 221  IF A$ = "2" THEN DX=5
 222  IF A$ = "3" THEN DY=-5
 223  IF A$ = "4" THEN DY=5
 230  X = X + DX : Y = Y + DY
 231  IF Y < 0 THEN Y = Y - DY
 232  IF X < 0 THEN X = X - DX
 233  IF X > 279 THEN X = X - DX
 234  IF Y > 191 THEN Y = Y - DY
 250  RETURN

 300  REM CHANGE BRUSH
 320  INPUT "BRUSH: ";NS
 330  IF NS < 0 THEN 320
 340  IF NS > MV THEN 320
 360  HOME: RETURN

 400  REM DRAW AT POINT
 420  GOSUB 5000
 430  DRAW NS AT X,Y
 450  RETURN

 500  REM CHANGE COLOR
 510  PRINT "NEW COLOR: ";: GET A$
 520  C =  VAL (A$)
 530  PRINT : IF C > 7 THEN 510 : IF C < 0 THEN 510
 540  HCOLOR = C
 570  HOME: RETURN

 600  REM LINE MODE LOOP
 601  HOME
 605  VTAB 21: XDRAW NS AT X,Y
 610  PRINT "LINE MODE"
 611  PRINT "X1=";A;"  Y1=";B;"   X=";X;"   Y=";Y;"  "
 612  PRINT "BYTES USED: ";BY - 19456;"   "
 615  GET A$: XDRAW NS AT X,Y
 620  IF A$ = "1" THEN  GOSUB 200: REM MOVE 5
 621  IF A$ = "2" THEN  GOSUB 200
 622  IF A$ = "3" THEN  GOSUB 200
 623  IF A$ = "4" THEN  GOSUB 200
 630  IF A$ = "Y" THEN  GOSUB 200: REM MOVE 1
 631  IF A$ = "G" THEN  GOSUB 200
 632  IF A$ = "J" THEN  GOSUB 200
 633  IF A$ = " " THEN  GOSUB 200
 640  IF A$ = "C" THEN  GOSUB 500: REM COLOR
 650  IF A$ = "@" THEN  GOSUB 700: REM START LINE
 660  IF A$ = "!" THEN  GOSUB 800: REM DRAW LINE
 670  IF A$ = "R" THEN 699: REM RETURN TO POINT MODE
 680  GOTO 605
 699  HOME: RETURN

 700  REM START LINE
 710  A = X:B = Y
 720  X = X + 10
 730  IF X > 279 THEN X = X - 20
 740  XDRAW NS AT A,B
 750  RETURN

 800  REM DRAW LINE
 810  XDRAW NS AT A,B
 820  HPLOT A,B TO X,Y
 830  GOSUB 5200
 840  RETURN

 900  REM FILL MODE LOOP
 901  A = X
 910  HOME
 920  VTAB 21: XDRAW 1 AT A,Y: XDRAW 1 AT X,Y
 021  PRINT "FILL MODE"
 922  PRINT A;"  ";X;"  ";Y;"  BYTES USED: ";BY - 19456;"  "
 925  GET A$: XDRAW 1 AT A,Y: XDRAW 1 AT X,Y
 930  IF A$ = "C" THEN GOSUB 500: REM COLOR
 932  IF A$ = "A" THEN A = A - 1
 933  IF A < 0 THEN A = A + 1
 934  IF A$ = "D" THEN A = A + 1
 935  IF A > 279 THEN A = A - 1
 936  IF A$ = "G" THEN X = X - 1
 937  IF X < 0 THEN X = X + 1
 938  IF A$ = "J" THEN X = X + 1
 939  IF X > 279 THEN X = X - 1
 940  IF A$ = "Y" THEN GOSUB 960: REM FILL UP ONE LINE
 941  IF A$ = " " THEN GOSUB 960: REM FILL DOWN ONE LINE
 942  IF A$ = "Z" THEN GOSUB 1000: REM REPEATING FILL DOWN
 949  IF A$ = "R" THEN 999: REM RETURN TO POINT MODE
 950  GOTO 920
 960  IF A$ = "Y" THEN Y = Y - 1
 961  IF Y < 0 THEN Y = Y + 1
 962  IF A$ = " " THEN Y = Y + 1
 963  IF Y > 159 THEN Y = Y - 1
 970  B = Y: GOSUB 820
 973  RETURN
 999  HOME: RETURN

 1000  REM REPEATING FILL DOWN
 1005  INPUT "DOWN HOW MANY: ";ZA
 1045  FOR I = Y TO Y + ZA: HPLOT A,I TO X,I: NEXT I
 1046  Y = Y + ZA
 1099  HOME: RETURN

 1100  REM REDRAW
 1110  POKE BY,96: REM TERMINATION CODE
 1119  REM SETUP CALLBACK FOR INTERPRETER TO LOAD ADDRESS OF DATA
 1120  POKE 768,189: POKE 769,0: POKE 770,76: POKE 771,96: REM LDA,X 00 4C RTS
 1120  CALL 19200: REM INVOKE INTEPRETER
 1199  RETURN

 1200  REM UNDO ONE
 1210  BY = BY - 8
 1220  GOSUB 1100
 1299  RETURN

 1300  REM UNDO MANY
 1310  INPUT "UNDO HOW MANY STEPS: ";S
 1320  BY = BY - (S * 8)
 1330  GOSUB 1100
 1399  HOME: RETURN

 1400  REM REWIND
 1410  INPUT "REWIND AND INSERT AT STEP# ";S
 1420  OLD = BY
 1430  BY = 19456 + (S * 8)
 1440  GOSUB 1100
 1499  HOME: RETURN

 1500  REM FAST FORWARD
 1510  BY = OLD
 1520  GOSUB 1100
 1599  RETURN

 1600  REM DRAW PARTIAL PICTURE
 1610  INPUT "DRAW HOW MANY STEPS? ";S
 1620  OLD = BY
 1630  BY = 19456 + (S * 8)
 1640  BN =  PEEK (BY)
 1650  GOSUB 1100
 1660  GET A$
 1670  POKE BY,BN: BY = OLD
 1680  GOSUB 1100
 1699  HOME: RETURN

 1700  REM LINEAR REPEAT SHAPE
 1710  DRAW NS AT X,Y: GOSUB 5000
 1720  INPUT "STEP UP/DOWN: ";ZD
 1730  INPUT "STEP RIGHT/LEFT: ";ZE
 1740  INPUT "NUMBER OF REPS: ";ZF
 1750  FOR I = 1 TO ZF
 1760 X = X + ZE: Y = Y + ZD: IF X > 279 THEN 1790
 1761  IF X < 0 THEN 1790
 1762  IF Y < 0 THEN 1790
 1763  IF Y > 191 THEN 1790
 1770  DRAW NS AT X,Y: GOSUB 5000: NEXT
 1780  HOME: RETURN
 1790 X = X - ZE: Y = Y - ZD: HOME: RETURN

 1800  REM PICTURE BUFFER
 1810  GOSUB 3000
 1820  PRINT CHR$(4);"BLOAD ";A$;",A$2000,S";S;",D";D;",V";V
 1830  RETURN

 3000  REM CHOOSE MASS STORAGE
 3001  HOME : VTAB 21: INPUT "FILENAME: ";A$
 3005  INPUT "SLOT (6): ";B$
 3010  S =  VAL (B$)
 3020  IF B$ = "" THEN S = 6
 3030  INPUT "DRIVE (1): ";B$
 3040  D =  VAL (B$)
 3050  IF B$ = "" THEN D = 1
 3060  INPUT "VOLUME (254): ";B$
 3070  V =  VAL (B$)
 3080  IF B$ = "" THEN V = 254
 3085  INPUT "PRESS ENTER WHEN READY";B$
 3090  RETURN

 5000  REM ENCODE A SHAPE
 5010  IF X > 255 THEN 5030
 5020  POKE BY,0: POKE BY + 1,C: POKE BY + 2,NS: POKE BY + 3,X: POKE BY + 4,0: POKE BY + 5,Y: GOTO 5040
 5030  POKE BY,0: POKE BY + 1,C: POKE BY + 2,NS: POKE BY + 3,X - 256: POKE BY + 4,1: POKE BY + 5,Y
 5040 BY = BY + 8: RETURN

 5100  REM ENCODE LOAD SHAPE TABLE 0
 5110  POKE BY,3: POKE BY + 1,176
 5120  BY = BY + 8: RETURN

 5200  REM ENCODE LINE
 5210  POKE BY,1: POKE BY + 5,B: POKE BY + 6,Y: POKE BY + 7,C
 5220  IF A > 255 THEN 5240
 5230  POKE BY + 1,A: POKE BY + 2,0: GOTO 5250
 5240  POKE BY + 1,A - 256: POKE BY + 2,1
 5250  IF X > 255 THEN 5270
 5260  POKE BY + 3,X: POKE BY + 4,0: GOTO 5280
 5270  POKE BY + 3,X - 256: POKE BY + 4,1
 5280  BY = BY + 8: RETURN

 5300  REM ENCODE FILL DOWN
 5310  POKE BY,2: POKE BY + 5,Y: POKE BY + 6,ZA + 2: POKE BY + 7,C
 5320  IF A > 255 THEN 5340
 5330  POKE BY + 1,A: POKE BY + 2,0: GOTO 5350
 5340  POKE BY + 1,A - 256: POKE BY + 2,1
 5350  IF X > 255 THEN 5370
 5360  POKE BY + 3,X: POKE BY + 4,0: GOTO 5380
 5370  POKE BY + 3,X - 256: POKE BY + 4,1
 5380  BY = BY + 8: RETURN

 8000  REM LOAD EXISTING GRAPHIC
 8030  GOSUB 3000
 8035  PRINT  CHR$ (4);"BLOAD ";A$;",A";BY;",S";S;",D";D;",V";V;"
 8040  BY = 19456
 8042  X =  PEEK (BY)
 8044  IF X = 96 THEN 8050
 8046  BY = BY + 8: GOTO 8042
 8050  GOSUB 1100: HOME: RETURN

 30000  INPUT "DO YOU REALLY WANT TO QUIT ? ";A$
 30001  IF A$ = "Y" THEN 30004
 30002  RETURN
 30004  INPUT "SAVE ? ";A$
 30005  IF A$ = "Y" THEN 30100
 30010  GOTO 30160
 30100  GOSUB 3000
 30110  POKE BY,96:BY = BY + 2
 30155  PRINT : PRINT : PRINT  CHR$ (4);"BSAVE ";A$;",A19456,L";BY - 19456;",S";S;",D";D;",V";V;""
 30160  PRINT "INSERT SDP IN DRIVE 1"
 30170  INPUT "PRESS ENTER WHEN READY";A$
 30180  PRINT : PRINT : PRINT CHR$(4);"RUN HELLO,D1"
