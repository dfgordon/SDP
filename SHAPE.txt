 0  ST$ = "S6,D1,V254"
 10  HCOLOR= 3: SCALE= 1: ROT= 0: TA = 16384
 15  REM POKE IN A SHAPE TO USE AS A CURSOR
 20  POKE 768,1: POKE 769,0: POKE 770,4: POKE 771,0
 30  POKE 772,58: POKE 773,36: POKE 774,45: POKE 771,0
 40  TEXT : HOME : PRINT "CHOOSE:": PRINT
 50  PRINT "L--> LOAD A NEW SHAPE TABLE ": PRINT
 60  PRINT "N--> START NEW SHAPE TABLE": PRINT
 70  PRINT "YOUR CHOICE "; CHR$ (95);: HTAB 12: INPUT A$
 80  IF A$ = "N" THEN 180
 90  IF A$ <  > "L" THEN 40
 100  GOSUB 3000
 110  PRINT  CHR$ (4);"BLOAD ";A$;",A$4000,S";S;",D";D;",V";V
 120 N = ( PEEK (TA + 2) + 256 *  PEEK (TA + 3) - 2) / 2
 130 SN =  PEEK (TA): PRINT : IF SN = N THEN  PRINT "TABLE FULL": END
 140  PRINT "THIS TABLE CAN HOLD ";N;"SHAPES": PRINT
 150  PRINT "IT PRESENTLY HAS ";SN;" SHAPE(S) IN IT. ": PRINT
 160  FOR I = 1 TO 2500: NEXT I
 170  GOTO 240
 180  TEXT : HOME : INPUT "NUMBER OF SHAPES IN THIS TABLE :";N
 190  POKE TA,0: POKE TA + 1,0
 200 DA = 2 * N + 2
 210  POKE TA + 2,DA - 256 *  INT (DA / 256)
 220  POKE TA + 3, INT (DA / 256)
 230  FOR I = TA + 4 TO TA + 2 * N + 3: POKE I,0: NEXT I
 240  PRINT : PRINT "CHOOSE SIZE OF SHAPE DESIGN GRID "
 250  INPUT "COLUMNS ";C: C = C * 4: REM RIGHTMOST PIXEL
 260  INPUT "ROWS ";R: R = 150 - 4 * R: REM TOPMOST PIXEL
 270  HGR
 280  FOR I = R TO 150 STEP 4: HPLOT 0,I TO C,I: NEXT I
 290  FOR I = 0 TO C STEP 4: HPLOT I,R TO I,150: NEXT I
 310  HOME : VTAB 21: PRINT "STARTING POSITION ? LOWER LEFT IS (1,1) "
 320  INPUT "COLUMN ";X1: INPUT "ROW ";Y1
 330 X = 4 * X1 - 2:Y = 156 - 4 * Y1
 340 ADDR =  PEEK (TA + SN * 2 + 2) + 256 *  PEEK (TA + SN * 2 + 3) + TA
 350  POKE 232,0: POKE 233,3
 360  XDRAW 1 AT X,Y:P = 0:V =  - 1
 370  HOME : VTAB 21: PRINT "TABLE CAPACITY:";N;" SHAPES -THIS IS # ";SN + 1
 380  PRINT "MOVE=Y,G,J,SPACE  PLOT=P"
 390  PRINT "QUIT THIS SHAPE = Q"
 410  GET V$
 420  IF V$ = "Q" THEN  POKE ADDR,P: POKE ADDR + 1,255: GOTO 520
 430  XDRAW 1 AT X,Y
 440  IF V$ = "P" THEN P = 4: FOR I = 1 TO 3: HPLOT X - 1,Y + (I - 2) TO X + 1,Y + (I - 2): NEXT I: XDRAW 1 AT X,Y: GOTO 410
 450  IF V$ = "Y" THEN V = P:Y = Y - 4: IF Y < R THEN Y = Y + 4: PRINT  CHR$ (7);:V =  - 1
 460  IF V$ = "J" THEN V = P + 1:X = X + 4: IF X > C THEN X = X - 4: PRINT  CHR$ (7);:V =  - 1
 470  IF V$ = " " THEN V = P + 2:Y = Y + 4: IF Y > 150 THEN Y = Y - 4: PRINT  CHR$ (7);:V =  - 1
 480  IF V$ = "G" THEN V = P + 3:X = X - 4: IF X < 0 THEN X = X + 4: PRINT  CHR$ (7);:V =  - 1
 490  IF V =  - 1 THEN  XDRAW 1 AT X,Y: GOTO 410
 500  POKE ADDR,V:ADDR = ADDR + 1
 510  XDRAW 1 AT X,Y:P = 0:V =  - 1: GOTO 410
 520 ADDR =  PEEK (TA + SN * 2 + 2) + 256 *  PEEK (TA + SN * 2 + 3) + TA:BYTE = ADDR
 530 C1 =  PEEK (BYTE): IF C1 = 255 THEN  POKE ADDR,0:ADDR = ADDR + 1: GOTO 660
 540 C2 =  PEEK (BYTE + 1): IF C2 = 255 THEN  POKE ADDR,C1: POKE ADDR + 1,0:ADDR = ADDR + 2: GOTO 660
 550 C3 =  PEEK (BYTE + 2): IF C3 = 255 THEN  POKE ADDR + 2,C1 + 8 * C2: POKE ADDR + 1,0:ADDR = ADDR + 2: GOTO 660
 560 CODE = C1 + 8 * C2 + 64 * C3
 570  IF CODE = 0 THEN  POKE ADDR,64: POKE ADDR + 1,24:ADDR = ADDR + 1:BYTE = BYTE + 2: GOTO 650
 580  IF CODE < 8 THEN  POKE ADDR,CODE: GOTO 650
 590  IF CODE < 64 THEN  POKE ADDR,CODE:BYTE = BYTE + 1: GOTO 650
 600  IF CODE < 256 THEN  POKE ADDR,CODE:BYTE = BYTE + 2: GOTO 650
 610 CODE = CODE - 64 * C3
 620  IF CODE = 0 THEN  POKE ADDR,64: POKE ADDR + 1,3:ADDR = ADDR + 1:BYTE = BYTE + 1: GOTO 650
 630  IF CODE < 8 THEN  POKE ADDR,CODE: GOTO 650
 640  POKE ADDR,CODE:BYTE = BYTE + 1
 650 ADDR = ADDR + 1:BYTE = BYTE + 1: GOTO 530
 660  POKE TA,SN + 1: POKE 232,000: HGR : POKE 233,64: DRAW SN + 1 AT 200,100
 661 ZN = SN + 1
 670  HOME : VTAB 22: PRINT "SAVE THIS AS SHAPE NUMBER ";SN + 1;" (Y/N) ";: INPUT A$
 680  IF A$ = "Y" THEN SN = SN + 1: IF SN < N THEN DA = ADDR - TA: POKE TA + 2 * SN + 2,DA - 256 *  INT (DA / 256): POKE TA + 2 * SN + 3, INT (DA / 256)
 690  IF A$ <  > "N" AND A$ <  > "Y" THEN 670
 700  POKE TA,SN
 710  TEXT : HOME : VTAB 17: PRINT "CHOOSE:"
 720  PRINT  TAB( 5);"1. DRAW ANOTHER SHAPE."
 730  PRINT  TAB( 5);"2. SAVE SHAPE TABLE TO DISK."
 740  PRINT  TAB( 5);"3. EXIT PROGRAM."
 750  INPUT A$:A =  VAL (A$)
 760  IF A < 1 OR A > 3 THEN 710
 770  IF SN = N AND A = 1 THEN  PRINT "TABLE FULL": FOR I = 1 TO 1000: NEXT I: GOTO 710
 780  ON A GOTO 240,800,900

 800  REM SAVE TO DISK
 810  GOSUB 3000
 820  PRINT  CHR$ (4);"BSAVE ";A$;",A$4000,L";ADDR - 16384;",S";S;",D";D;",V";V
 830  GOTO 710

 900  REM EXIT BACK TO SDP MAIN
 910  TEXT : HOME : PRINT "PUT SDP IN ";ST$
 915  INPUT "PRESS ENTER WHEN READY";A$
 920  PRINT : PRINT CHR$(4);"RUN HELLO,";ST$

 3000  REM CHOOSE MASS STORAGE
 3001  HOME : VTAB 21: INPUT "FILENAME: ";A$
 3005  INPUT "SLOT (6): ";B$
 3010  S =  VAL (B$)
 3020  IF B$ = "" THEN S = 6
 3030  INPUT "DRIVE (1): ";B$
 3040  D =  VAL (B$)
 3050  IF B$ = "" THEN D = 1
 3060  INPUT "VOLUME (254): ";B$
 3070  V =  VAL (B$)
 3080  IF B$ = "" THEN V = 254
 3085  INPUT "PRESS ENTER WHEN READY";B$
 3090  RETURN
