 0  ST$ = "S6,D1,V254"
 1  POKE 232,0: POKE 233,64: REM SHAPE ADDR
 2  SCALE=1: ROT=0
 3  HOME : HGR
 4  VTAB 21
 5  INPUT "IS DRAWING SQUEEZED (Y/N) ? ";SQ$
 10  IF SQ$="Y" THEN PRINT CHR$(4);"BLOAD SDP.INTRP.E,";ST$
 20  IF SQ$="N" THEN PRINT CHR$(4);"BLOAD SDP.INTRP,";ST$
 30  PRINT  CHR$ (4);"BLOAD D0,A$4000,";ST$ : REM SHAPES
 40  GOSUB 2000
 50  INPUT "INCLUDE FRAME (Y/N) ? ";FR$
 100  REM MAIN LOOP
 110  INPUT "FILE (Q=QUIT)? ";A$
 120  IF A$="Q" THEN GOTO 400
 125  HGR
 130  IF FR$="Y" THEN GOSUB 200
 140  GOSUB 300
 150  GOTO 100

 200  REM DRAW FRAME
 210  PRINT  CHR$ (4);"BLOAD FRAMEM,A$310,S";SL$;",D";DV$;",V";VOL$
 220  A1=16: A2=3: CLR=0: GOSUB 1000
 230  RETURN

 300  REM DRAW GRAPHIC
 310  PRINT  CHR$ (4);"BLOAD ";A$;",A$4C00,S";SL$;",D";DV$;",V";VOL$
 320  A1=0: A2=76: CLR=0: GOSUB 1000
 330  RETURN

 400  TEXT : HOME
 410  PRINT "INSERT SDP IN ";ST$
 420  INPUT "PRESS ENTER WHEN READY";A$
 430  PRINT : PRINT : PRINT CHR$(4);"RUN HELLO,";ST$

 1000  REM INVOKE SDP INTERPRETER
 1010  POKE 768,189: POKE 769,A1: POKE 770,A2: POKE 771,96: REM LDA,X A1 A2 RTS
 1020  IF CLR THEN CALL 19200
 1030  IF NOT CLR THEN CALL 19203
 1040  RETURN

 2000  REM GET DRIVE INFORMATION
 2010  INPUT "ENTER SLOT (6) ";SL$
 2020  IF SL$="" THEN SL$="6"
 2030  INPUT "ENTER DRIVE (1) ";DV$
 2040  IF DV$="" THEN DV$="1"
 2050  INPUT "ENTER VOLUME (254) ";VOL$
 2060  IF VOL$="" THEN VOL$="254"
 2070  RETURN
