0  ST$ = "S6,D1,V254": SCALE= 1: ROT= 0: POKE 232,0: POKE 233,64: GOTO 80

1  TEXT : HOME: REM SELECT FONT
2  SCALE= 1: ROT= 0
3  ? "SELECT A FONT:": ? "(1) BOLD": ? "(2) TECH": ? "(3) CALIGRAPHY": ? "(4) PICA": ? "(5) STANDARD"
4  GET A$
5 A =  VAL (A$)
6  IF A > 5 THEN 1
7  IF A < 1 THEN 1
8  PRINT : PRINT  CHR$ (4);"BLOAD D";A;",A$4000,";ST$
9  IF A = 1 THEN FT = 7
10  IF A = 1 THEN FY = 8
11  IF A = 2 THEN FT = 10
12  IF A = 2 THEN FY = 11
13  IF A = 3 THEN FT = 16
14  IF A = 3 THEN FY = 16
15  IF A = 4 THEN FT = 5
16  IF A = 4 THEN FY = 6
17  IF A = 5 THEN FT = 7
18  IF A = 5 THEN FY = 8
19  IF A = 7 THEN FT = 6
20  IF A = 7 THEN FY = 8
21  IF A = 3 THEN PRINT "WARNING: FONT INCOMPLETE ";: GET A$
25  RETURN

80  HGR : VTAB 21
81  PRINT  CHR$ (4);"BLOAD SDP.INTRP,A$4B00,";ST$
82  HOME : VTAB 21: INPUT "BACKGROUND PICTURE ? ";A$
83  IF A$ = "Y" THEN 8000
84  GOSUB 1: NS = 1: X = 1: Y = FY: BY = 19456
87  POKE  - 16304,0
90  HOME: VTAB 21

100  XDRAW NS AT X,Y
110  HTAB 1: VTAB 21: PRINT "X=";X;"  Y=";Y;"  CTRL-Q QUITS": CALL -868
111  HTAB 1: VTAB 22: PRINT "BYTES USED: ";BY - 19456;: CALL -868
120  GET A$
121  IF A$ =  CHR$ (8) THEN  GOSUB 200
122  IF A$ =  CHR$ (21) THEN  GOSUB 200
123  IF A$ =  CHR$ (10) THEN  GOSUB 200
124  IF A$ =  CHR$ (11) THEN  GOSUB 200
130  IF A$ =  CHR$ (13) THEN  GOSUB 200
131  IF A$ = "@" THEN 60000
132  IF A$ = "#" THEN 60000
133  IF A$ = " " THEN  GOSUB 200
134  IF  ASC (A$) > 64 AND  ASC (A$) < 91 THEN 250
135  IF  ASC (A$) > 48 AND  ASC (A$) < 58 THEN 400
136  IF A$ = ":" THEN 410
137  IF A$ = "," THEN 420 
138  IF A$ = "." THEN 430
139  IF A$ = "0" THEN 405
140  IF A$ = "-" THEN 440
141  IF A$ = "!" THEN 450
144  IF A$ = "?" THEN 460
145  IF A$ = "+" THEN 480
146  IF A$ = "=" THEN 470
151  IF A$ = "]" THEN  POKE  - 16302,0
152  IF A$ = "[" THEN  POKE  - 16301,0
153  IF A$ = CHR$(17) THEN 30000: REM QUIT
155  IF A$ = CHR$(3) THEN 290: REM COLOR
157  IF A$ = CHR$(127) THEN 900: REM UNDO
199  VTAB 21: GOTO 110

200  XDRAW NS AT X,Y: REM POSITIONING
205  IF  ASC (A$) = 8 THEN X = X - FT
206  IF  ASC (A$) = 21 THEN X = X + FT
210  IF A$ =  CHR$ (10) THEN Y = Y + FY
211  IF A$ =  CHR$ (11) THEN Y = Y - FY
215  IF A$ =  CHR$ (13) THEN X = 1: Y = Y + FY: HOME
230  IF A$ = " " THEN X = X + FT
231  IF X < 0 THEN Y = Y - FY
232  IF X < 0 THEN X = X + FT
233  IF Y < 0 THEN Y = Y + FY
234  IF X > 279 THEN Y = Y + FY
235  IF X > 279 THEN X = 1
236  IF Y > 191 THEN Y = Y - FY
240  XDRAW NS AT X,Y: RETURN

250  XDRAW NS AT X,Y: REM ALPHA ENTRY
252 NP =  ASC (A$)
253 NP = NP - 63
271  GOSUB 311
275  DRAW NP AT X,Y
279 A$ =  CHR$ (21): GOSUB 205
280  VTAB 21: GOTO 110

290  HOME : VTAB 21: INPUT "NEW COLOR ?";C
299  XDRAW NS AT X,Y
300  IF C > 7 THEN 290
301  IF C < 0 THEN 290
302  HCOLOR= C
310  HOME : VTAB 21: XDRAW NS AT X,Y: GOTO 110

311  REM ENCODE PLOT SHAPE
320  IF X > 255 THEN 322
321  POKE BY,0: POKE BY + 1,C: POKE BY + 2,NP: POKE BY + 3,X: POKE BY + 4,0: POKE BY + 5,Y: GOTO 329
322  POKE BY,0: POKE BY + 1,C: POKE BY + 2,NP: POKE BY + 3,X - 256: POKE BY + 4,1: POKE BY + 5,Y
329 BY = BY + 8: VTAB 21: RETURN

400  XDRAW NS AT X,Y: REM NUMBER ENTRY
401 NP =  ASC (A$):NP = NP - 21
402  GOTO 271

405  XDRAW NS AT X,Y
406 NP = 37
407  GOTO 271

410  XDRAW NS AT X,Y
411 NP = 38
412  GOTO 271

420  XDRAW NS AT X,Y
421 NP = 39
422  GOTO 271

430  XDRAW NS AT X,Y
431 NP = 40
432  GOTO 271

440  XDRAW NS AT X,Y
441 NP = 41
442  GOTO 271

450  XDRAW NS AT X,Y
451 NP = 42
452  GOTO 271

460  XDRAW NS AT X,Y
461 NP = 43
462  GOTO 271

470  XDRAW NS AT X,Y
471 NP = 45
472  GOTO 271

480  XDRAW NS AT X,Y
481 NP = 44
482  GOTO 271

800  HOME : VTAB 21: INPUT "FILENAME: ";A$: REM FILE PICKER
801  INPUT "SLOT: ";B$
802 S =  VAL (B$)
803  IF B$ = "" THEN S = 6
804  INPUT "DRIVE: ";B$
805 D =  VAL (B$)
806  IF B$ = "" THEN D = 1
807  INPUT "VOLUME: ";B$
808 V =  VAL (B$)
809  IF B$ = "" THEN V = 254
810  RETURN

900 BY = BY - 8: REM UNDO
905  POKE BY,96
908  PRINT : PRINT : PRINT
909  POKE 768,189: POKE 769,0: POKE 770,76: POKE 771,96
910  CALL 19200
920  HOME : VTAB 21: GOTO 100

8000  HOME : VTAB 21: INPUT "(P)ICTURE BUFFER OR (S)DP FILE ? ";A$
8001  IF A$ = "S" THEN 8030
8002  GOSUB 800
8005  PRINT  CHR$ (4);"BLOAD ";A$;",A$2000,S";S;",D";D;",V";V;""
8006  GOTO 82
8030  GOSUB 800
8035  PRINT  CHR$ (4);"BLOAD ";A$;",A$4C00,S";S;",D";D;",V";V;""
8036  POKE 768,189: POKE 769,0: POKE 770,76: POKE 771,96: CALL 19203
8039  GOTO 82

30000  PRINT: INPUT "DO YOU REALLY WANT TO QUIT ? ";A$
30001  IF A$ = "Y" THEN 30004
30002  GOTO 110
30004  INPUT "SAVE ? ";A$
30005  IF A$ = "Y" THEN 30100
30010  GOTO 30130
30100  GOSUB 800
30110  POKE BY,96:BY = BY + 2
30120  PRINT  CHR$ (4);"BSAVE ";A$;",A19456,L";BY - 19456;",S";S;",D";D;",V";V
30130  PRINT "INSERT SDP IN ";ST$
30140  INPUT "PRESS ENTER WHEN READY";A$
30150  PRINT  CHR$ (4);"RUN HELLO,";ST$

60000  XDRAW NS AT X,Y: REM NUDGE
60001  IF A$ = "@" THEN Y = Y - 1
60002  IF A$ = "#" THEN X = X - 1
60005  XDRAW NS AT X,Y: VTAB 21: GOTO 110
